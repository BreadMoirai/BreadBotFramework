/*
 *        Copyright 2017-2018 Ton Ly (BreadMoirai)
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 */

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
//    id 'com.sedmelluq.jdaction' version '1.0.2'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
    id 'signing'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'org.ajoberstar.git-publish' version '3.0.0-rc.1'
    id 'jacoco'
}

final author = 'BreadMoirai'
final artifactId = 'breadbot-framework'
group = "com.github.${author.toLowerCase()}"
version = '0.13.1'
final versionDesc = ""
//project.hasProperty never seems to work properly
def props = new Properties()
file("private.properties").with(false) { file ->
    if (file.exists()) {
        file.withInputStream { is ->
            props.load(is)
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://dl.bintray.com/dv8fromtheworld/maven"
    }
}

dependencies {
    //Discord Java Library
    compileOnly 'net.dv8tion:JDA:4.2.0_225'

    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'org.reflections:reflections:0.9.10'
    implementation 'com.typesafe:config:1.3.2'

    implementation 'javax.inject:javax.inject:1'
    implementation 'net.sf.trove4j:trove4j:3.0.3'

//    compile 'org.jetbrains:annotations:16.0.1'

    testImplementation 'uk.org.lidalia:slf4j-test:1.1.0'
    testImplementation('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testImplementation 'net.dv8tion:JDA:4.2.0_225'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.+'
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC

    exclude 'com/github/breadmoirai/breadbot/framework/internal'
}

githubRelease {
    token = props.getProperty('github')
    String version = project.version.toString()
    def title = ""
    it.releaseName = "$version $title"
    it.body = "## Changes\n$versionDesc\n## Adding as Dependency\n### Gradle\n```gradle\nrepositories {\n  jcenter()\n}\n\ndependencies {\n  compile '$project.group:$artifactId:$project.version'\n}\n```\n\n### Maven\n```xml\n<repository>\n  <id>jcenter</id>\n  <name>jcenter</name>\n  <url>http://jcenter.bintray.com/</url>\n</repository>\n\n<dependency>\n  <groupId>$project.group</groupId>\n  <artifactId>$artifactId</artifactId>\n  <version>$project.version</version>\n  <type>pom</type>\n</dependency>\n```"
    releaseAssets = jar.destinationDirectory.asFileTree.filter { file -> file.name.contains(version) }
    overwrite = true
//    println releaseAssets.findAll()
}

gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'https://github.com/BreadMoirai/BreadBotFramework.git'
    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    contents {
        from javadoc.destinationDir
    }

    commitMessage = "Javadoc Ver ${project.version} on ${new Date()}" // defaults to 'Generated by gradle-git-publish'
}

gitPublishReset {
    dependsOn javadoc
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.java.srcDirs
}

bintray {
    user = author.toLowerCase()
    key = props.getProperty('bintray')
    publications = ["BintrayRelease"]
    publish = true
    pkg {
        repo = 'maven'
        name = artifactId
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/${author}/${project.name}.git"
        version {
            name = project.version
            released = new Date()
            desc = versionDesc
        }
    }
}

publishing {
    publications.create('BintrayRelease', MavenPublication) {
        from components.java
        it.groupId = project.group as String
        it.artifactId = artifactId
        it.version = version
        artifact javadocJar
        artifact sourcesJar
        artifact shadowJar
    }
}

compileJava.options.encoding = 'UTF-8'

shadowJar.archiveClassifier.set 'withDependencies'
javadoc.source = sourceSets.main.allJava
javadoc.failOnError = false

task jarMake {
    dependsOn jar
    dependsOn sourcesJar
    dependsOn javadocJar
    dependsOn shadowJar
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
        csv.enabled = false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}